Simple Tactics
==============

Move duders around and have them hit one another.

Duder
-----

    templates =
      duder: require "./duder_template"

    gameTemplate = require "./game_template"

    names = [
      "Carmack"
      "Romero"
      "Hall"
    ]

    enemyNames = [
      "Rush"
      "Bjork"
      "Pantera"
    ]

    alive = (o) ->
      o.I.hp > 0

    msg = (msg) ->
      events.push "#{msg}\n"

    attack = (from, to) ->
      if from and to
        dmg = [0..1].map ->
          1.d(6) > 1.d(6)
        .sum()

        if dmg > 0
          msg "#{from.name()} struck #{to.name()} for #{dmg}!"

          to.hp(to.hp() - dmg)
        else
          msg "#{from.name()} missed #{to.name()}"


    Duder = (I={}, self=Model(I)) ->
      Object.defaults I,
        active: false
        enemy: false
        index: 0
        hp: 3
        hpMax: 3
        
      I.name ?= names[I.index]

      self.observeAll()

      self.click = ->
        if I.enemy
          if activeDuder = guys.filter (guy) ->
            guy.active()
          .first()
            activeDuder.active(false)

            attack(activeDuder, self)

            if opponent = opponents.filter(alive).first()
              if target = guys.filter(alive).rand()
                attack(opponent, target)
              else
                msg "You have been defeated!"
            else
              msg "Yo, you da winner!"

        else
          if self.hp() > 0
            self.activate()

      self.activate = ->
        guys.invoke "active", false
        self.active(true)

      return self

    opponents = [0..2].map (i) ->
      Duder
        name: enemyNames[i]
        enemy: true

    guys = []

    guys = [0..2].map (i) ->
      Duder
        index: i

    events = Observable []

    $('body').append gameTemplate
      duders: guys
      opponents: opponents
      render: (template, object) ->
        templates[template](object)
      events: events

    $('body').append $ "<style>",
      text: require('./style')
